# -*- mode: emacs-lisp -*-
# key: template
# name: Template (my skeleton for various LaTeX documents)
# type: command
# --
;; Snippet format: <https://capitaomorte.github.io/yasnippet/snippet-development.html>.

;; Useful LaTeX links:
;;   * Nice font combinations: <http://tex.stackexchange.com/questions/9533/what-best-combination-of-fonts-for-serif-sans-and-mono-do-you-recommend>.
;;   * Some style tips: <http://tex.stackexchange.com/questions/82376/why-are-these-commands-considered-as-bad-practice>.

(require 'cl-lib) ; Used: cl-flet.
(let* ((yas-good-grace nil) ; Don't catch errors.
       (engine (completing-read "TeX engine: "
                                '("XeTeX" "pdfTeX")
                                nil nil nil nil "XeTeX"))
       (document-class (completing-read "Document class: "
                                        '("scrartcl" "article" "amsart" "letter" "beamer")
                                        nil nil nil nil "article"))
       (engine-auctex (pcase engine
                        ("XeTeX" "xetex")
                        ("pdfTeX" "default")))
       (document-body-expr "`yas-selected-text`$0"))
  (cl-flet* ((smart-comment (line)
                            (if (or (string-prefix-p "%" line)
                                    (string-empty-p line))
                                line
                              (concat "%" line)))
             (join-lines (function lines)
                         (let ((non-nil-lines (delq nil lines)))
                           (when non-nil-lines
                             (mapconcat function non-nil-lines "\n"))))
             (latex-block (transformation &rest lines)
                          (pcase transformation
                            (`ignore nil)
                            (`smart-comment (join-lines #'smart-comment lines))
                            (`comment (join-lines (lambda (line) (concat "%" line)) lines))
                            (`verbatim (join-lines #'identity lines)))))
    (yas-expand-snippet
     (latex-block
      'verbatim

      ;; Warnings.
      "\\RequirePackage[l2tabu, orthodox]{nag} % Style warnings in .log file (grep for \"nag\")."
      ""

      ;; Document type, margins, etc.
      (pcase document-class
        ("scrartcl"
         "\\documentclass[12pt, a4paper, parskip=full]{scrartcl} % Margins: DIV=<number> (larger means more content; default for 12pt is 12). Periods after section numbers: numbers=endperiod.")
        ((or "article" "amsart" "letter")
         (concat "\\documentclass[12pt]{" document-class "}"))
        (_
         (concat "\\documentclass{" document-class "}")))
      (when (member document-class '("article" "amsart" "letter"))
        (latex-block 'verbatim
                     "\\usepackage[a4paper]{geometry} % Setting margins: \"top=5cm\" (later options have more priority)."
                     "\\usepackage{parskip} % Space instead of indent between paragraphs."))
      (when (string= document-class "beamer")
        (latex-block 'verbatim
                     "\\usetheme[block=fill]{metropolis}"
                     ""
                     "% Use the Metropolis theme along with another Beamer color theme."
                     "%\\useoutertheme{metropolis}"
                     "%\\useinnertheme{metropolis}"
                     "%\\usefonttheme{metropolis}"
                     "%\\usecolortheme{seahorse}"))
      ""

      "\\usepackage{microtype} % Better typography." ; No longer needs to be loaded after fonts and babel (unless using "babel=true"). See <http://tex.stackexchange.com/questions/162137/loading-microtype-before-or-after-the-font>.
      (when (not (string= document-class "beamer")) ; Enumitem conflicts with beamer.
        "\\usepackage{enumitem} % Add features to \\`itemize' and \\`enumerate'.")
      ""

      "% Alternative fonts."
      (latex-block 'smart-comment
                   "\\usepackage{fontspec}"
                   "\\setmainfont{Linux Libertine O} % Alternatives: Computer Modern (the default), TeX Gyre Pagella (with TeX Gyre Pagella Math), Cambria (with Cambria Math)."
                   "\\setsansfont{Carlito} % Free replacement for Calibri. Alternatives: Linux Biolinum O, Open Sans (the default for scrartcl)."
                   "\\setmonofont{Consolas} % Alternatives: DejaVu Sans Mono, Inconsolata."
                   "\\setsansfont{Gillius ADF} % Free replacement for Gill Sans. Alternatives: Sans Guilt MB, Sans Guilt DB, Cabin, Lato."
                   "")

      "% Suppress page numbers if there's only one page."
      "\\usepackage{zref-totpages}"
      "\\AtBeginDocument{\\ifnum\\ztotpages=1{\\pagenumbering{gobble}}\\fi}"
      ""

      "% Language."
      (if (equal engine "XeTeX")
          (latex-block 'verbatim
                       "\\usepackage{polyglossia}" ; Just to be safe, load it near the beginning of the preamble (see <http://tex.stackexchange.com/questions/150788/should-babel-package-call-be-placed-at-the-end-of-the-preamble>).
                       "\\setmainlanguage{${polish}}"
                       "%\\setotherlanguage{${english}}")
        (latex-block 'verbatim "\\usepackage[${polish}, ${english}]{babel}"))
      (when (equal engine "pdfTeX")
        (latex-block 'verbatim
                     "\\usepackage[utf8]{inputenc}"
                     "\\usepackage[T1]{fontenc}"))
      ""

      "% Quotes (language-aware)."
      (latex-block 'verbatim
                   "\\usepackage[autostyle]{csquotes} % Example: \\enquote{text}"
                   "\\DeclareQuoteStyle{polish}{\\quotedblbase}{\\textquotedblright}[0.05em]{\\textquoteleft}{\\textquoteright}"
                   "")

      "% Math."
      (latex-block 'verbatim
                   "\\usepackage[all]{onlyamsmath} % Add improved math macros, disable standard ones."
                   "\\usepackage{amsfonts} % E.g. \\`mathbb'.")
      "% Make \\`mathbb' work for numbers."
      (latex-block 'verbatim
                   "\\usepackage{dsfont} % Blackboard bold that includes numbers. Requires \\`texlive-fonts-extra'."
                   "\\usepackage{xstring} % For \\`IfInteger'."
                   "\\let\\oldmathbb\\mathbb"
                   "\\renewcommand{\\mathbb}[1]{\\IfInteger{#1}{\\mathds{#1}}{\\oldmathbb{#1}}}")
      "% Use bold instead of arrows for vectors."
      (latex-block 'smart-comment
                   "\\let\\oldvec\\vec"
                   "\\renewcommand{\\vec}[1]{\\boldsymbol{\\mathbf{#1}}}")
      ""

      "% Tables."
      ;; Overview of table packages: <http://tex.stackexchange.com/questions/12672/which-tabular-packages-do-which-tasks-and-which-packages-conflict>.
      (latex-block 'verbatim
                   "\\usepackage{tabu} % Featureful replacement for \\`tabular'."
                   "\\usepackage{booktabs} % Elegant tables (use \\\\{top,mid,bottom}rule instead of \\hline; no \\vline)." ; `booktabs' examples: <http://www.howtotex.com/packages/improve-your-tables-with-booktabs/>.
                   "")

      "% Graphics."
      (latex-block 'verbatim
                   "\\usepackage{graphicx} % See <https://www.sharelatex.com/learn/Inserting_Images>. Remember to place \\`label' last inside the \\`figure'. Example: \\begin{figure}[h] \\centering \\includegraphics[width=0.5\\textwidth]{spiral} \\caption{Spiral.} \\label{fig:spiral} \\end{figure}"
                   "\\usepackage{float} % For the \\`H' positioning specifier."
                   "\\usepackage{placeins} % Provides \\FloatBarrier, which prevents floats before it from moving past it."
                   "\\usepackage{grffile} % Correctly handle complex file names."
                   "\\graphicspath{{images/}}"
                   "\\newcommand{\\credit}[1]{\\par\\tiny\\textit{#1}}"
                   "\\usepackage{subfig} % Subfigures (for e.g. arranging figures in a grid)."
                   (when (string= document-class "beamer")
                     (latex-block 'verbatim
                                  "\\captionsetup[figure]{labelformat=empty} % Disable \"Figure <number>\" prefixes."
                                  "\\newcommand{\\includegraphicsFullSlide}[1]{"
                                  "  \\includegraphics[width=0.9\\textwidth,height=0.8\\textheight,keepaspectratio]{#1}}"))
                   "")

      "% Drawings."
      (latex-block 'verbatim
                   "\\usepackage{tikz}"
                   "\\usetikzlibrary{babel} % Fixes conflict with onlyamsmath (see <http://tex.stackexchange.com/questions/31860/conflict-onlyamsmath-and-tikz>)."
                   "")

      "% Pseudocode."
      (latex-block 'verbatim
                   "\\usepackage[noend]{algpseudocode} % \\`algorithmicx' with some predefined commands."
                   (when (string= document-class "beamer")
                     "\\newenvironment{algorithmBlock}{\\begin{block}{Algorytm}}{\\end{block}}")
                   "% Continuation lines (usage: \\State \\begin{varwidth}[t]{\\linewidth} line1 \\continuationLine line2 ... \\end{varwidth})."
                   "\\usepackage{varwidth}"
                   "\\newcommand{\\continuationLine}{\\par\\hskip\\algorithmicindent}"
                   "")

      (when (string= document-class "beamer")
        (latex-block 'verbatim
                     "% Outline slides."
                     "\\newcommand{\\tocFrame}[1][currentsection, currentsubsection]{ % Highlight the current section and subsection by default. To disable, pass an empty parameter. To highlight section only, remove \\`currentsubsection'. To highlight all subsections of the current section, add \\`subsectionstyle=show/show/shaded'."
                     "  \\metroset{background=dark}"
                     "  \\begin{frame}[noframenumbering, plain]"
                     "	  \\frametitle{Plan prezentacji}"
                     "	  \\tableofcontents[#1]"
                     "  \\end{frame}"
                     "  \\metroset{background=light}}"
                     "% Use the outline slides instead of section and subsection title slides."
                     "%\\AtBeginSection[]{\\tocFrame{}}"
                     "%\\AtBeginSubsection[]{\\tocFrame{}}"
                     "% Disable section and subsection title slides for more manual control."
                     "%\\AtBeginSection[]{}"
                     "%\\AtBeginSubsection[]{}"
                     ""))

      (when (string= document-class "beamer")
        (latex-block 'verbatim
                     "% Localize theorem environments."
                     "% Theorem environments provided by Beamer: theorem, corollary, definition, definitions, fact, example, examples, lemma. You can also use \\`\\begin{block}{Theorem} ... \\end{block}'."
                     "\\deftranslation[to=polish]{Theorem}{Twierdzenie}"
                     "\\deftranslation[to=polish]{Definition}{Definicja}"
                     "\\theoremstyle{plain}\\newtheorem{observation}{Obserwacja}"
                     "\\theoremstyle{definition}\\newtheorem*{proofIdea}{Idea dowodu}"
                     ""))

      "% Bibliography."
      (latex-block 'smart-comment
                   "\\usepackage[backend=biber]{biblatex}"
                   "\\addbibresource{`(when (buffer-file-name) (file-name-sans-extension (file-name-nondirectory (buffer-file-name))))`.bib}"
                   (when (string= document-class "beamer")
                     (latex-block 'smart-comment
                                  "% Use icons; automatically choose the icon depending on type of the source."
                                  "\\setbeamertemplate{bibliography item}{%"
                                  "  \\ifboolexpr{test {\\ifentrytype{book}} or test {\\ifentrytype{mvbook}}"
                                  "	or test {\\ifentrytype{collection}} or test {\\ifentrytype{mvcollection}}"
                                  "	or test {\\ifentrytype{reference}} or test {\\ifentrytype{mvreference}}}"
                                  "  {\\setbeamertemplate{bibliography item}[book]}"
                                  "  {\\ifentrytype{online}"
                                  "	{\\setbeamertemplate{bibliography item}[online]}"
                                  "	{\\setbeamertemplate{bibliography item}[article]}}%"
                                  "  \\usebeamertemplate{bibliography item}}"
                                  "% Use triangles, numbers or nothing instead of icons."
                                  "%\\setbeamertemplate{bibliography item}[triangle]"
                                  "%\\setbeamertemplate{bibliography item}[text]"
                                  "%\\setbeamertemplate{bibliography item}{}"))
                   "")

      "% Comments."
      (latex-block 'verbatim
                   "\\usepackage{color}"
                   "\\usepackage{soul}"
                   "\\setulcolor{red}"
                   "\\newcommand{\\comment}[1]{\\ul{[#1]}}"
                   "\\usepackage{ifthen}"
                   "\\newcommand{\\todo}[1]{\\ifthenelse{\\equal{#1}{}}{\\comment{TODO}}{\\comment{TODO: #1}}} % \\`ul' has to be called inside \\`ifthenelse'; the reverse produces an error: \"Argument of \\ifthenelse has an extra }\"."
                   "% Block comments (also for calling attention to document areas)."
                   "\\usepackage{mdframed}"
                   "\\newenvironment{blockComment}{\\begin{mdframed}[topline=false, leftline=true, bottomline=false, rightline=false, linecolor=red]}{\\end{mdframed}}"
                   "%\\renewenvironment{blockComment}[1]{}{} % Temporarily disable."
                   "")

      ;; Title, author, etc.
      (pcase document-class
        ("letter" (latex-block 'verbatim
                               "\\signature{${`(user-full-name)`}}"
                               "\\address{${}}"))
        (_ (latex-block 'verbatim
                        "\\title{${Document title}}"
                        "\\author{${`(user-full-name)`}}"
                        "\\date{}")))
      ""

      ;; Document body.
      "\\begin{document}"
      (pcase document-class
        ("letter" (latex-block 'verbatim
                               "\\begin{letter}{${Destination address}}"
                               "\\opening{${Dear Sir or Madam,}}"
                               ""
                               document-body-expr
                               ""
                               "\\closing{Yours Respectfully,}"
                               "\\end{letter}"))
        (_ (latex-block 'verbatim
                        "\\maketitle"
                        ""
                        document-body-expr
                        ""
                        "% Bibliography."
                        (if (string= document-class "beamer")
                            (latex-block 'smart-comment
                                         "\\begin{frame}[allowframebreaks]{\\bibname} % \\bibname is localized \"bibliography\", \\refname is localized \"references\"."
                                         "\\nocite{*} % Show all sources (even uncited ones)."
                                         "\\printbibliography[heading=none]"
                                         "\\end{frame}")
                          (latex-block 'smart-comment
                                       "\\nocite{*} % Show all sources (even uncited ones)."
                                       "\\printbibliography")))))
      "\\end{document}"
      ""

      ;; AUCTeX data.
      (when engine-auctex
        (latex-block 'verbatim
                     "% Local Variables:"
                     (concat "% TeX-engine: " engine-auctex)
                     "% ispell-local-dictionary: \"polish\""
                     "% End:"
                     ""))))))
